from pathlib import Path
import glob
import pandas as pd
import numpy as np
from collections import defaultdict
from snakemake.io import directory
import os

######## RUN THIS TO CREATE DAG ########
# snakemake --cores 4 --dag 2>/dev/null | grep -v "Including" | dot -Tpdf > dag.pdf
########################################


configfile: "config.yaml"

ar6_clim_factors = ['prec','temp_min','temp_max','temp_avg']
gwl_scenarios = ['GWL1.5','GWL2.0','GWL3.0','GWL4.0']
ssp_scenarios = ['historical','ssp126','ssp245','ssp370','ssp585']
branches = config["branches"]

# Locate all WORK directories 
SSPRCP_MAIN_DIR = Path(workflow.basedir).resolve()
SSPRCP_BASE_DIR = SSPRCP_MAIN_DIR.parents[0]
SCRIPTS_DIR = SSPRCP_MAIN_DIR / "scripts"


ruleorder:
    GWL_convert_and_merge_across_years > GWL_merge_across_models

###############################################################################
# Directories
###############################################################################

SSPRCP_WORK_DIR = SSPRCP_BASE_DIR / "work"
DATA_DIR = SSPRCP_WORK_DIR / "data"
RAW_DIR = DATA_DIR / "raw"
INTERIM_DIR = DATA_DIR / "interim"
PROCESSED_DIR = DATA_DIR / "processed"

GWL_RAW_DIR   = RAW_DIR / "GWL"
AR6_RAW_DIR   = RAW_DIR / "AR6"
AR6_INTERIM_DIR = INTERIM_DIR / "AR6"
TREAD_RAW_DIR = RAW_DIR / "TREAD"
NETCDF_DIR    = PROCESSED_DIR / "NetCDF"
GWL_NETCDF_DIR = NETCDF_DIR / "GWL_NetCDF"
AR6_NETCDF_DIR = NETCDF_DIR / "AR6_NetCDF"
TREAD_NETCDF_DIR = NETCDF_DIR / "TREAD_NetCDF"

FIGURES_DIR = SSPRCP_WORK_DIR / "figures"

# Create directories
dirs_to_create = [
    RAW_DIR,
    INTERIM_DIR,
    AR6_INTERIM_DIR,
    PROCESSED_DIR,
    GWL_RAW_DIR,
    AR6_RAW_DIR,
    TREAD_RAW_DIR,
    NETCDF_DIR,
    GWL_NETCDF_DIR,
    AR6_NETCDF_DIR,
    TREAD_NETCDF_DIR,
    FIGURES_DIR
]
for directory in dirs_to_create:
    directory.mkdir(parents=True, exist_ok=True)

###############################################################################
# Rule all: final targets
###############################################################################
rule all:
    input:
        GWL_NETCDF_DIR / "gwl_all.nc",
        GWL_NETCDF_DIR / "gwl_all_sliced.nc",
        GWL_NETCDF_DIR / "gwl_all_sliced.tsv"


###############################################################################
# Include branch rules
###############################################################################
for branch in branches:
    print(f"Including rules for branch: {branch}")
    include: f"rules/{branch}.smk"

###############################################################################
# Rule: Merge CSVs â†’ One NetCDF per (clim_factor, gwl)
###############################################################################
# Helper function to collect CSVs safely
def collect_csvs(wc):
    folder = Path(GWL_RAW_DIR) / wc.clim_factor / wc.gwl
    if not folder.exists():
        raise FileNotFoundError(f"Folder does not exist: {folder.resolve()}")
    csv_files = sorted(folder.glob("*.csv"))
    if not csv_files:
        raise ValueError(f"No CSV files found in {folder.resolve()}")
    return csv_files


rule GWL_convert_and_merge_across_years:
    input:
        lambda wc: collect_csvs(wc)
    output:
        GWL_NETCDF_DIR / "{clim_factor}" / "{gwl}.nc"
    conda:
        SSPRCP_MAIN_DIR / "envs/environment.yaml"
    script:
        SCRIPTS_DIR / "GWL_convert_and_merge_across_years.py"

###############################################################################
# Rule: Combine all GWL NetCDFs for each climate factor
###############################################################################

rule GWL_merge_across_models:
    input:
        lambda wc: expand(
            GWL_NETCDF_DIR / wc.clim_factor / "{gwl}.nc",
            gwl=gwl_scenarios
        )
    output:
        GWL_NETCDF_DIR / "combined_{clim_factor}.nc"
    conda:
        SSPRCP_MAIN_DIR / "envs/environment.yaml"
    script:
        SCRIPTS_DIR / "GWL_merge_across_models.py"

###############################################################################
# Rule: Merge all climate factors into a single gwl_all.nc
###############################################################################

rule GWL_merge_across_clim_factors:
    input:
        expand(GWL_NETCDF_DIR / "combined_{clim_factor}.nc", clim_factor=ar6_clim_factors)
    output:
        GWL_NETCDF_DIR / "gwl_all.nc"
    conda:
        SSPRCP_MAIN_DIR / "envs/environment.yaml"
    script:
        SCRIPTS_DIR / "GWL_merge_across_clim_factors.py"


###############################################################################
# Rule: Slice GWL all.nc depending on provided configuration
###############################################################################

rule GWL_get_slice_netcdf:
    input:
        GWL_NETCDF_DIR / "gwl_all.nc"
    output:
        GWL_NETCDF_DIR / "gwl_all_sliced.nc"
    conda:
        SSPRCP_MAIN_DIR / "envs/environment.yaml"
    script:
        SCRIPTS_DIR / "GWL_get_slice_netcdf.py"


###############################################################################
# Rule: Convert sliced NetCDF to TSV
###############################################################################
rule GWL_netcdf_slice_to_tsv:
    input:
        GWL_NETCDF_DIR / "gwl_all_sliced.nc"
    output:
        GWL_NETCDF_DIR / "gwl_all_sliced.tsv"
    conda:
        SSPRCP_MAIN_DIR / "envs/environment.yaml"
    script:
        SCRIPTS_DIR / "GWL_netcdf_slice_to_tsv.py"

